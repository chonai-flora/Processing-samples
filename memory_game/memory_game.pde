final float screen = 960;
final float halfScreen = screen / 2;

int turn = +1;
int cardCount = 52;
int cardX = -1, cardY = -1;
int[] score = new int[2];
ArrayList < Trump > trumps = new ArrayList < Trump > ();

// Processingã§ã¯æ§‹é€ ä½“ãŒä½¿ãˆãªã„ãŸã‚
// å¿…è¦ãªå¤‰æ•°ã‚’ã¾ã¨ã‚ãŸTrumpã‚¯ãƒ©ã‚¹ã‚’å®šç¾©ã™ã‚‹
class Trump {
  int mark;
  int number, subNumber;
  boolean hidden, subHidden;
  String card;
  float x, y;

  Trump(int mark, int number, boolean hidden, String card, float x, float y) {
    // 0:ã‚¹ãƒšãƒ¼ãƒ‰ 1:ãƒãƒ¼ãƒˆ 2:ãƒ€ã‚¤ãƒ¤ãƒ¢ãƒ³ãƒ‰ 3:ã‚¯ãƒ­ãƒ¼ãƒãƒ¼
    this.mark = mark;
    // A~Kã¾ã§ã®æ•°å­—
    this.number = number;
    this.subNumber = -1;
    // ã‚«ãƒ¼ãƒ‰ã®æŸ„ãŒéš ã‚Œã¦ã„ã‚‹ã‹ã©ã†ã‹
    this.hidden = hidden;
    this.subHidden = false;
    // ãƒˆãƒ©ãƒ³ãƒ—ã®çµµæ–‡å­—
    this.card = card;
    // ãƒˆãƒ©ãƒ³ãƒ—ã®XYåº§æ¨™
    this.x = x; this.y = y;
  }

  void setNumber() {
    this.number = this.subNumber;
    this.subNumber = -1;
  }

  void setHidden() {
    this.hidden = true;
    this.subHidden = false;
  }

  void draw() {
    fill(this.hidden || this.mark < 1 || this.mark > 2
      ? #000000 : #FF0000
      );
    text(
      this.hidden ? "ğŸ‚ " : this.card,
      this.x, this.y
      );
  }
}

int toIndex(int r, int c) { // åº§æ¨™ã‚’ãƒˆãƒ©ãƒ³ãƒ—ã®ã‚¤ãƒ³ãƒ‡ãƒƒã‚¯ã‚¹ã«å¤‰æ›
  return r % 13 + 12 * c + c;
}

int getNumber(int r, int c) { // æŒ‡å®šã—ãŸåº§æ¨™ã®ãƒˆãƒ©ãƒ³ãƒ—ã®numberã‚’å–å¾—
  return trumps.get(toIndex(r, c)).number;
}

void showScore(int n) { // ã‚¹ã‚³ã‚¢è¡¨ç¤º
  push();
  float x = halfScreen * (10 * n + 1) / 6 + 16;
  float y = halfScreen * (11 - 10 * n) / 6 + 90;
  fill(n == 1 ? #00C000 : #0000FF);
  textSize(halfScreen / 8);
  text("ğŸ‚ ", x - halfScreen / 8, y);
  fill(#000000);
  textSize(halfScreen / 12);
  text("Ã—" + nf(score[n], 2), x, y);
  pop();
}

void showTurn() { // ã‚¿ãƒ¼ãƒ³è¡¨ç¤º
  push();
  textAlign(LEFT, CENTER);
  textSize(halfScreen/10);
  boolean gameEnds = cardCount <= 0;
  String message = gameEnds ? "win!" : "'s turn";

  if (gameEnds && score[0] == score[1]) {
    text("Draw", 2, 90 - halfScreen / 12);
    pop();
    return;
  }
  boolean fillStatus = gameEnds ? score[0] < score[1] : turn == 1;
  fill(#000000);
  text(message, halfScreen / 6 - 6, 90 - halfScreen / 12);
  fill(fillStatus ? #00C000: #0000FF);
  textSize(halfScreen / 6);
  text("ğŸ‚ ", 8, 80 - halfScreen / 12);
  pop();
}

void setCards() { // ãƒˆãƒ©ãƒ³ãƒ—ã‚’ã‚»ãƒƒãƒˆ
  IntList numbers= new IntList();
  // range() çš„ãªãƒ¡ã‚½ãƒƒãƒ‰ãŒä½¿ãˆã‚‹ãªã‚‰ãã£ã¡ã§
  // ä»Šå›ã¯forã§ä»£ç”¨
  for (int i = 0; i < cardCount; i++) {
    numbers.append(i);
  }
  numbers.shuffle();
  for (int i = 0; i < 4; i++) {
    for (int j = 0; j <= 13; j++) {
      if (j == 11) continue;
      int k = j > 11 ? j - 1 : j;
      int n = numbers.get(toIndex(k, i));
      int m = n % 13;
      int o = n / 13;
      // ãƒˆãƒ©ãƒ³ãƒ—ã®çµµæ–‡å­—ã‚’ã²ã¨ã¤ã²ã¨ã¤ç”¨æ„ã™ã‚‹ã®ã¯å¤§å¤‰ãªã®ã§
      // Character.toChars() ã‚’ç”¨ã„ã¦Unicodeã‚³ãƒ¼ãƒ‰ãƒã‚¤ãƒ³ãƒˆã‹ã‚‰ç”Ÿæˆ
      trumps.add(new Trump(
        o,
        m + 1,
        true,
        new String(Character.toChars(
        0x01F0A1 + 16 * o + m + int(m > 10)
        )),
        screen / 13 * k + halfScreen / 13,
        screen / 8 * (i + k / 3.0 + 0.5) + 90
        ));
    }
  }
}

void setup() {
  size(960, 1080);
  textAlign(CENTER, CENTER);
  textFont(createFont("Segoe UI Symbol", screen / 9));
  setCards();
  noLoop();
}

void draw() {
  background(#FFFFFF);
  showTurn();
  showScore(0);
  showScore(1);

  for (Trump trump : trumps) {
    push();
    if (trump.number > 0) {
      trump.draw();
    }
    pop();

    if (trump.subNumber != -1) {
      trump.setNumber();
    }
    if (trump.subHidden) {
      trump.setHidden();
    }
  }
}

void mousePressed() {
  int p = floor(13 * mouseX / screen);
  int q = floor((12 * (mouseY - 90) - p * halfScreen) / (3 * halfScreen));

  if (p < 0 || p > 13 || q < 0 || q > 3) return;

  Trump trump = trumps.get(toIndex(p, q));
  if (trump.number < 1) return;
  trump.hidden = false;

  if (cardX < 0 && cardY < 0) {
    cardX = p; cardY = q;
  } else {
    if (p != cardX || q != cardY) {
      boolean cardsMatched =
        getNumber(p, q) == getNumber(cardX, cardY);
      if (cardsMatched) {
        score[int(turn == 1)] += 2;
        cardCount -= 2;
        trumps.get(toIndex(p, q)).subNumber =
          trumps.get(toIndex(cardX, cardY)).subNumber = 0;
      } else {
        turn = -turn;
        trumps.get(toIndex(p, q)).subHidden =
          trumps.get(toIndex(cardX, cardY)).subHidden = true;
      }
      cardX = cardY = -1;
    }
  }

  redraw();
}
